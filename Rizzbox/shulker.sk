options:
    prefix: &6&lShulker &7| &d

on place of any shulker box:
    if "%region at player%" contains "shulkerarea":
        set name of event-block to "%player%'s Shulker"
        add event-block to {shulkers::%player's uuid%::*}
        send "{@prefix}You have placed a shulker!"
    else:
        send "{@prefix}You can only place shulkers in the shulker room!"
        cancel event

on inventory click:
    if event-inventory != player's inventory:
        if name of player's inventory contains "Shulker":
            if name of event-inventory does not contain "%player%'s Shulker":
                if player does not have permission "shulker.spy":
                    cancel event

on right click:
    if event-block is any shulker box:
        if player does not have permission "shulker.spy":
            if {shulkers::%player's uuid%::*} does not contain event-block:
                cancel event
                send "{@prefix}That is not your shulker box!"
        else:
            set {_w} to name of event-block
            send "{@prefix}You have opened %{_w}%!"

on break of any shulker box:
    if player does not have permission "shulker.spy":
        if {shulkers::%player's uuid%::*} does not contain event-block:
            cancel event
            send "{@prefix}That is not your shulker box!"
        else:
            remove event-block from {shulkers::%player's uuid%::*}
            
command /clearshulkers:
    permission: shulker.clear
    trigger:
        broadcast "{@prefix}The shulker room is clearing..."
        wait 1 second
        loop all blocks within {loc1} and {loc2}:
            if loop-block is any shulker box:
                set loop-block to air
                delete {shulkers::*}

command /setshulkerroom:
    permission: op
    trigger:
        set {smode::%player's uuid%} to 1
        send "{@prefix}Break block number one"

on break:
    if {smode::%player's uuid%} is 1:
        cancel event
        set {loc1} to location of event-block
        set {smode::%player's uuid%} to 2
        send "{@prefix}Break block number two"
        stop
    if {smode::%player's uuid%} is 2:
        cancel event
        set {loc2} to location of event-block
        send "{@prefix}Shulker Room created."
        send "%{loc1}%,%{loc2}%"
        delete {smode::%player's uuid%}
        stop

on place:
    if player does not have permission "shulker.admin":
        if "%region at event-block%" contains "shulkerarea":
            if event-block is not any shulker box:
                cancel event

on break:
    if player does not have permission "shulker.admin":
        if "%region at event-block%" contains "shulkerarea":
            if event-block is not any shulker box:
                cancel event

on pickup:
    if "%region at player%" contains "shulkerarea":
        if event-item is any shulker box:
            if uncolored name of event-item does not contain "%player%'s Shulker" or "Gear" or "Kit":
                if player does not have permission "shulker.admin":
                    cancel event